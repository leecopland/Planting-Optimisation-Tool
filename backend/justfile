set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
export PYTHONUTF8 := "1"
export PYTHONPATH := "."

SERVICE_NAME      := "db"
CONTAINER_NAME    := "pot_postgres_db"
ALEMBIC_CMD       := "uv run alembic"
PYTEST_RUNNER     := "uv run"
PYTEST_CMD        := PYTEST_RUNNER + " pytest"
UV_RUNNER         := "uv run"
FASTAPI_TARGET    := "src/main.py"
FASTAPI_CMD       := "fastapi dev " + FASTAPI_TARGET + " --port 8080 --host 0.0.0.0"

default: help

[private]
ensure-env:
    #!python
    import os
    import shutil
    if not os.path.exists('.env'):
        if os.path.exists('.env.example'):
            shutil.copy('.env.example', '.env')
            print('--- Created .env from .env.example ---')
        else:
            print('ERROR: .env.example not found!')
            exit(1)

help:
    @just --list

setup: ensure-env db-teardown db-start db-migrate
    @echo "--- Setup/Update Complete ---"

db-teardown:
    @echo "--- Stopping PostgreSQL Container (Preserving Data) ---"
    @docker compose down
    @echo "--- PostgreSQL Container Stopped ---"

db-start:
    @echo "--- Starting PostgreSQL Container ---"
    @docker compose up -d {{SERVICE_NAME}}
    @echo "Waiting for DB to be ready..."
    @python -c "import time; time.sleep(5)"

db-migrate: ensure-env
    @echo "--- Applying Schema Migration ---"
    @{{ALEMBIC_CMD}} upgrade head
    @echo "--- Schema Migrated ---"

db-reset:
    @echo "--- WARNING: Wiping Database and Volumes ---"
    @docker compose down -v
    @just db-start

revision message: ensure-env
    @echo "--- Generating New Migration Script ---"
    @{{ALEMBIC_CMD}} revision --autogenerate -m "{{message}}"
    @echo "--- Migration Script generated---"

rename-column: ensure-env
    @echo "--- STARTING MANUAL RENAME PROCEDURE ---"
    @{{ALEMBIC_CMD}} revision -m "MANUAL_RENAME_REQUIRED"

test: ensure-env
    @echo "--- Running Pytest ---"
    @{{PYTEST_CMD}} tests/

psql: ensure-env
    @docker exec -it {{CONTAINER_NAME}} psql -U postgres -d POT_db

schema: ensure-env
    @echo "--- Generating SCHEMA.md from Models ---"
    @{{UV_RUNNER}} python -m src.generate_schema > SCHEMA.md

erd: ensure-env
    @echo "--- Generating ERD.md ---"
    @python -c "print('# Entity Relationship Diagram\n\n```mermaid')" > ERD.md
    @{{UV_RUNNER}} paracelsus graph src.database:Base --import-module "src.models:*" >> ERD.md
    @python -c "print('```')" >> ERD.md
    @echo "--- ERD.md created successfully. ---"

run-api: ensure-env
    -@{{UV_RUNNER}} {{FASTAPI_CMD}}

populate: ensure-env
    @echo "Preparing data ingestion environment..."
    @just db-reset
    @just db-migrate
    @{{UV_RUNNER}} python -m src.scripts.seed_references
    @{{UV_RUNNER}} python -m src.scripts.create_test_user
    @{{UV_RUNNER}} python -m src.scripts.setup_import_db
    @just kill-api
    @echo "Database is ready and populated. To start dev server run:"
    @echo "just run-api"
    

kill-api:
    @echo "Attempting to stop API server..."
    @{{UV_RUNNER}} python -m src.scripts.kill-api

audit:
    @echo "--- STARTING CROSS-PLATFORM AUDIT ---"
    @echo "1. Checking Environment..."
    @-just ensure-env
    @echo "2. Checking Database Migration (Alembic)..."
    @-{{ALEMBIC_CMD}} current
    @echo "3. Checking ER-Diagram Generation (Postgres/Psycopg)..."
    @-just erd
    @echo "4. Checking Schema Documentation..."
    @-just schema
    @echo "5. Checking Test Suite (Pytest)..."
    @-just test
    @echo "6. Checking API Entry Point..."
    @powershell.exe -Command "Start-Process uv -ArgumentList 'run', 'fastapi', 'run', 'src/main.py', '--port', '8080' -WindowStyle Hidden; Write-Host 'API starting, waiting 5s for warm-up'; Start-Sleep -s 5"
    @echo "7. Checking Kill Script..."
    @-just kill-api
    @echo "--- AUDIT COMPLETE ---"